/*
 * Copyright (c) 2016 Jean Guyomarc'h
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the "Software"),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
 * OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

externals {
   external: "elm";
}

collections {
   group { "war2edit/main";
      parts {
         swallow { "war2edit.main.contents";
            desc { "default";
            }
         }

         rect { "event_catcher";
            desc { "default";
               rel.to: "war2edit.main.contents";
               rel2.relative: 0.0 0.0;
               color: 0 0 0 0;
               visible: 0;
            }
            desc { "active";
               inherit: "default";
               visible: 1;
               rel2.relative: 1.0 1.0;
            }
         }

         /*=== Unitselector ===*/
         external { "bg1";
            source: "elm/bg";
            desc { "default";
               rel.to: "war2edit.main.unitselector";
            }
         }
         swallow { "war2edit.main.unitselector";
            desc { "default"; /* aka. hidden */
               rel.to: "war2edit.main.contents";
               rel1.relative: 0.25 0.0;
               rel2.relative: 0.75 0.0;
            }
            desc { "visible";
               inherit: "default";
               rel2.relative: 0.75 0.3;
            }
         }

         /*=== Notifications ===*/
         external { "bg2";
            source: "elm/bg";
            desc { "default";
               rel.to: "war2edit.main.notif";
            }
         }
         text { "war2edit.main.notif";
            desc { "default"; /* aka. hidden */
               rel.to: "war2edit.main.contents";
               rel1.relative: 1.0 0.15;
               rel2.relative: 1.0 0.15;
               min: 1 32;
               text { font: "Sans"; size: 12;
               }
            }
            desc { "visible";
               inherit: "default";
               rel1.relative: 0.7 0.15;
            }
         }
      }

      programs {
         program { signal: "war2edit,notif,show"; source: "war2edit";
            action: STATE_SET "visible";
            target: "war2edit.main.notif";
            transition: SINUSOIDAL 1.0;
         }
         program { signal: "war2edit,notif,hide"; source: "war2edit";
            action: STATE_SET "default";
            target: "war2edit.main.notif";
            transition: ACCELERATE 0.5;
         }

         program { signal: "mouse,down,*"; source: "event_catcher";
            action: SIGNAL_EMIT "war2edit,unitselector,event" "war2edit";
         }
         program { signal: "war2edit,unitselector,show"; source: "war2edit";
            action: STATE_SET "active";
            target: "event_catcher";
            after: "unitselector,real,show";
         }
         program { name: "unitselector,real,show";
            action: STATE_SET "visible";
            target: "war2edit.main.unitselector";
            transition: SINUSOIDAL 1.0;
         }
         program { signal: "war2edit,unitselector,hide"; source: "war2edit";
            action: STATE_SET "default";
            target: "event_catcher";
            after: "unitselector,real,hide";
         }
         program { name: "unitselector,real,hide";
            action: STATE_SET "default";
            target: "war2edit.main.unitselector";
            transition: ACCELERATE 0.5;
         }
      }
   }

   group { "war2edit/cursor";
      images {
         image: "frame.png" COMP;
      }
      parts {
         image { "cursor"; nomouse;
            desc { "default";
               image {
                  normal: "frame.png";
                  border: 3 3 3 3;
               }
               min: 1 1;
               color: 255 255 255 255;
            }
            desc { state: "invalid";
               inherit: "default";
               color: 255 0 0 255;
            }
         }
      }
      programs {
         program { signal: "cursor,disabled"; source: "*";
            action: STATE_SET "invalid" 0.0;
            target: "cursor";
         }
         program { signal: "cursor,enabled"; source: "*";
            action: STATE_SET "default" 0.0;
            target: "cursor";
         }
      }
   }

   group { "war2edit/unitselector/sel";
      parts {
         rect { "pretty";
            desc { "default";
               color: 0 0 124 124;
            }
         }
      }
   }

   group { "war2edit/unitselector/widget";
      images {
         image: "placeholder.png" COMP;
      }
      parts {
         image { "war2edit.unitselector.icon"; nomouse;
            desc { "default";
               rel2.relative: 0.0 0.0;
               max: 46 38;
               min: 46 38;
               fixed: 1 1;
               align: 0.0 0.0;
               image.normal: "placeholder.png";
            }
         }
         text { "war2edit.unitselector.name"; nomouse;
            desc { "default";
               text { font: "Sans"; size: 12;
                  align: 0.5 0.5;
                  text: "UNIT NAME";
               }
               rel1 {
                  to: "war2edit.unitselector.icon";
                  relative: 1.0 0.0;
                  offset: 5 0;
               }
               rel2 {
                  to_y: "war2edit.unitselector.icon";
                  relative: 1.0 0.5;
               }
            }
         }
         text { "war2edit.unitselector.info"; nomouse;
            desc { "default";
               text { font: "Sans"; size: 8;
                  align: 0.5 0.5;
                  text: "UNIT Text";
               }
               rel1 {
                  to_x: "war2edit.unitselector.icon";
                  to_y: "war2edit.unitselector.name";
                  relative: 1.0 1.0;
                  offset: 5 0;
               }
               rel2 {
                  to_x: "war2edit.unitselector.name";
                  to_y: "war2edit.unitselector.icon";
                  relative: 1.0 1.0;
               }
               align: 0.0 1.0;
            }
         }
         swallow { "war2edit.unitselector.contents";
            desc { "default";
               rel1 {
                  to: "war2edit.unitselector.icon";
                  relative: 0.0 1.0;
               }
            }
         }
      }
   }
}
